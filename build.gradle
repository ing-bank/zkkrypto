plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'maven-publish'
}

Properties secrets = new Properties()
File secretsFile = file("$projectDir/local.properties")
if (secretsFile.exists()) {
    secretsFile.withInputStream { secrets.load(it) }
}


group = "com.ing.dlt"
def artifactId = 'zkkrypto'
version '1.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {

    publications {
        maven(MavenPublication) {
            groupId = "${group}"
            artifactId = "${artifactId}"
            version = "$version"

            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ing-bank/zkkrypto"
            credentials {
                username = secrets.getProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = secrets.getProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}